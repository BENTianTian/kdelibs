AC_MSG_CHECKING(whether to enable GSSAPI support)
AC_ARG_WITH(gssapi,
[  --with-gssapi=PATH      Set path for GSSAPI files [default=check]],
[ case "$withval" in
  yes)
    with_gssapi=CHECK
    ;;
  esac ],
[ with_gssapi=CHECK ]
)dnl

if test "x$with_gssapi" = "xCHECK" ; then
  with_gssapi=NOTFOUND
  KDE_FIND_PATH(krb5-config, KRB5_CONFIG, [${prefix}/bin ${exec_prefix}/bin /usr/bin /usr/local/bin /opt/local/bin], [
    AC_MSG_WARN([Could not find krb5-config])
  ])

  if test -n "$KRB5_CONFIG"; then
    GSSAPI_INCS="`$KRB5_CONFIG --cflags gssapi`"
    GSSAPI_LIBS="`$KRB5_CONFIG --libs gssapi`"
    if test "$USE_RPATH" = yes; then
      for args in $GSSAPI_LIBS; do
        case $args in
          -L*)
             GSSAPI_RPATH="$GSSAPI_RPATH $args"
             ;;
        esac
      done
      GSSAPI_RPATH=`echo $GSSAPI_RPATH | sed -e "s/-L/-R/g"`
    fi
    gssapi_incdir="$GSSAPI_INCS"
    gssapi_libdir="$GSSAPI_LIBS"
    with_gssapi=FOUND
    if $KRB5_CONFIG --vendor | grep "Massachusetts" > /dev/null; then
      gssapi_flavor=MIT
    else
      gssapi_flavor=HEIMDAL
    fi
  else
    search_incs="$kde_includes /usr/include /usr/local/include"
    AC_FIND_FILE(gssapi.h, $search_incs, gssapi_incdir)
    if test -r $gssapi_incdir/gssapi.h ; then
      test "x$gssapi_incdir" != "x/usr/include" && GSSAPI_INCS="-I$gssapi_incdir"
      with_gssapi=FOUND
    fi
    if test $with_gssapi = FOUND ; then
      with_gssapi=NOTFOUND
      for ext in la so sl a dylib ; do
        AC_FIND_FILE(libgssapi.$ext, $kde_libraries /usr/lib /usr/local/lib,
          gssapi_libdir)
        if test -r $gssapi_libdir/libgssapi.$ext ; then
          if test "x$gssapi_libdir" != "x/usr/lib" ; then
            GSSAPI_LIBS="-L$gssapi_libdir "
            test "$USE_RPATH" = yes && GSSAPI_RPATH="-R $gssapi_libdir"
          fi
          GSSAPI_LIBS="${GSSAPI_LIBS}-lgssapi -lkrb5 -lasn1 -lcrypto -lroken -lcrypt ${LIBRESOLV}"
          with_gssapi=FOUND
          gssapi_flavor=HEIMDAL
          break
        fi
      done
    fi
  fi
fi

case "$with_gssapi" in
no) AC_MSG_RESULT(no) ;;
framework)
  GSSAPI_LIBS="-Xlinker -framework -Xlinker GSSAPI"
  AC_DEFINE_UNQUOTED(HAVE_LIBGSSAPI, 1, [Define if you have GSSAPI libraries])
  GSSAPI_SUBDIR="gssapi"
  AC_MSG_RESULT(Apple framework)
  ;;
NOTFOUND) AC_MSG_RESULT(searched but not found) ;;
*)
  if test "x$with_gssapi" = "xFOUND" ; then
    msg="incs=$gssapi_incdir libs=$gssapi_libdir"
  else
    msg="$with_gssapi"
    GSSAPI_ROOT="$with_gssapi"
    if test "x$GSSAPI_ROOT" != "x/usr" ; then
      GSSAPI_INCS="-I${GSSAPI_ROOT}/include"
      GSSAPI_LIBS="-L${GSSAPI_ROOT}/lib "
      if test "$USE_RPATH" = "yes" ; then
        GSSAPI_RPATH="-R ${GSSAPI_ROOT}/lib"
      fi
    fi
    if test -f ${GSSAPI_ROOT}/include/gssapi/gssapi.h ; then
      gssapi_flavor=MIT
      GSSAPI_LIBS="${GSSAPI_LIBS}-lgssapi_krb5 -lkrb5 -lk5crypto -lcom_err ${LIBRESOLV}"
    else
      gssapi_flavor=HEIMDAL
      GSSAPI_LIBS="${GSSAPI_LIBS}-lgssapi -lkrb5 -lasn1 -lcrypto -lroken -lcrypt ${LIBRESOLV}"
    fi
  fi
  if test "x$gssapi_flavor" = "xMIT" ; then
    AC_DEFINE_UNQUOTED(GSSAPI_MIT, 1, [Define if you have the MIT Kerberos libraries])
  fi
  AC_DEFINE_UNQUOTED(HAVE_LIBGSSAPI, 1, [Define if you have GSSAPI libraries])
  AC_MSG_RESULT($msg)
  ;;
esac

AC_SUBST(GSSAPI_INCS)
AC_SUBST(GSSAPI_LIBS)
AC_SUBST(GSSAPI_RPATH)

dnl -------
dnl Test for libntlm (NTLM support)
dnl -------

AC_MSG_CHECKING(whether to enable NTLM support)
AC_ARG_WITH(ntlm,
[  --with-ntlm=PATH      Set path for NTLM files [default=check]],
[ case "$withval" in
  yes)
    with_ntlm=CHECK
    ;;
  esac ],
[ with_ntlm=CHECK ]
)dnl

if test "x$with_ntlm" = "xCHECK" ; then
  with_ntlm=NOTFOUND
  search_incs="$kde_includes /usr/include /usr/local/include"
  AC_FIND_FILE(ntlm.h, $search_incs, ntlm_incdir)
  if test -r $ntlm_incdir/ntlm.h ; then
    test "x$ntlm_incdir" != "x/usr/include" && NTLM_INCS="-I$ntlm_incdir"
    with_ntlm=FOUND
  fi
  if test $with_ntlm = FOUND ; then
    $with_ntlm=NOTFOUND
    for ext in la so sl a dylib ; do
      AC_FIND_FILE(libntlm.$ext, $kde_libraries /usr/lib /usr/local/lib,
        ntlm_libdir)
      if test -r $ntlm_libdir/libntlm.$ext ; then
        if test "x$ntlm_libdir" != "x/usr/lib" ; then
          NTLM_LIBS="-L$ntlm_libdir "
          test "$USE_RPATH" = yes && NTLM_RPATH="-R $ntlm_libdir"
        fi
        NTLM_LIBS="${NTLM_LIBS}-lntlm"
        with_ntlm=FOUND
        break
      fi
    done
  fi
fi

case "$with_ntlm" in
no) AC_MSG_RESULT(no) ;;
framework)
  NTLM_LIBS="-Xlinker -framework -Xlinker NTLM"
  AC_DEFINE_UNQUOTED(HAVE_LIBNTLM, 1, [Define if you have the NTLM library])
  NTLM_SUBDIR="ntlm"
  AC_MSG_RESULT(Apple framework)
  ;;
NOTFOUND) AC_MSG_RESULT(searched but not found) ;;
*)
  if test "x$with_ntlm" = "xFOUND" ; then
    msg="incs=$ntlm_incdir libs=$ntlm_libdir"
  else
    msg="$with_ntlm"
    NTLM_ROOT="$with_ntlm"
    if test "x$NTLM_ROOT" != "x/usr" ; then
      NTLM_INCS="-I${NTLM_ROOT}/include"
      NTLM_LIBS="-L${NTLM_ROOT}/lib "
      if test "$USE_RPATH" = "yes" ; then
        NTLM_RPATH="-R ${NTLM_ROOT}/lib"
      fi
    fi
    NTLM_LIBS="${NTLM_LIBS}-lntlm"
  fi
  AC_DEFINE_UNQUOTED(HAVE_LIBNTLM, 1, [Define if you have the NTLM library])
  AC_MSG_RESULT($msg)
  ;;
esac

AC_SUBST(NTLM_INCS)
AC_SUBST(NTLM_LIBS)
AC_SUBST(NTLM_RPATH)
