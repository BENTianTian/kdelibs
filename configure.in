# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.
AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM 
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdelibs, 2.0pre) dnl searches for some needed programs

AC_PROG_INSTALL

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
AC_LIBTOOL_DLOPEN
KDE_PROG_LIBTOOL

dnl find out some variables (not supported yet)
AC_AIX
AC_MINIX

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files, GNU_GETTEXT for the sources
AM_KDE_WITH_NLS
AM_KDE_GNU_GETTEXT  

dnl Checks for header files.
KDE_CHECK_STL
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/cdefs.h fnmatch.h sysent.h strings.h sys/stat.h sys/select.h sys/socket.h linux/socket.h socketbits.h sigaction.h paths.h malloc.h)

dnl Checks for libraries.
AC_BASE_PATH_KDE([don't test]) dnl kdelibs is a special case
AC_CREATE_KFSSTND(default)
AC_SUBST_KFSSTND
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD

dnl Image libraries
KDE_CREATE_LIBS_ALIASES
KDE_CHECK_KIMGIO

AM_CONDITIONAL(HAVE_LIBJPEG, test -n "$LIBJPEG" )
AM_CONDITIONAL(HAVE_LIBPNG, test -n "$LIBPNG" )
AM_CONDITIONAL(HAVE_LIBTIFF, test -n "$LIBTIFF")

# configure would do this very late. Too late for us!
test "x$prefix" = xNONE && prefix=$ac_default_prefix

AC_DEFINE_UNQUOTED(KDEDIR, "$prefix")

if test "x$exec_prefix" = xNONE; then
  ac_kde_exec_prefix=KDEDIR
else 
  ac_kde_exec_prefix=$exec_prefix
fi

AC_DEFUN(AC_DEFINE_PATH,
[
  eval "tempdir=`echo $2 | sed -e 's/\$(exec_prefix)/$ac_kde_exec_prefix/' -e 's/\$(prefix)/KDEDIR/'`"
  AC_DEFINE_UNQUOTED($1, "$tempdir")
])

# Let make expand exec_prefix.
test "x$exec_prefix" = xNONE && exec_prefix='$(prefix)' 

AC_DEFINE_PATH(KDE_HTMLDIR, $kde_htmldir)
AC_DEFINE_PATH(KDE_APPSDIR, $kde_appsdir)
AC_DEFINE_PATH(KDE_ICONDIR, $kde_icondir)
AC_DEFINE_PATH(KDE_SOUNDDIR, $kde_sounddir)
AC_DEFINE_PATH(KDE_DATADIR, $kde_datadir)
AC_DEFINE_PATH(KDE_LOCALE, $kde_locale)
AC_DEFINE_PATH(KDE_CGIDIR, $kde_cgidir)
AC_DEFINE_PATH(KDE_CONFIGDIR, $kde_confdir)
AC_DEFINE_PATH(KDE_MIMEDIR, $kde_mimedir)
AC_DEFINE_PATH(KDE_TOOLBARDIR, $kde_toolbardir)
AC_DEFINE_PATH(KDE_WALLPAPERDIR, $kde_wallpaperdir)
AC_DEFINE_PATH(KDE_BINDIR, $kde_bindir)
AC_DEFINE_PATH(KDE_PARTSDIR, $kde_partsdir)

ac_cpp_safe=$ac_cpp
ac_cpp='$CXXCPP $CPPFLAGS $X_INCLUDES'
AC_CHECK_HEADERS(X11/extensions/shape.h)
ac_cpp=$ac_cpp_safe

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
dnl AC_CHECK_GNU_EXTENSIONS

dnl Checks for library functions. 
AC_CHECK_FUNCS(socket vsnprintf seteuid setegid random)
AC_FUNC_VFORK
AC_CHECK_USLEEP
AC_CHECK_SETENV
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_RANDOM
AC_CHECK_S_ISSOCK
AC_CHECK_KSIZE_T

KDE_CHECK_MICO(2.2.6)
KDE_CHECK_PYTHON(1.5)

if test -n "$qt_includes"; then
  QNAMESPACE_H="$qt_includes/qnamespace.h"
fi
AC_SUBST(QNAMESPACE_H)

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl output files
topdir=`pwd`
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(topdir)
AC_SUBST(LIBSOCKET)
AC_SUBST(LIBCOMPAT)

KOM_INCLUDES='-I$(top_srcdir)/corba/kom -I$(top_builddir)/corba/kom'
AC_SUBST(KOM_INCLUDES)

PARTSUI_INCLUDES='-I$(top_srcdir)/corba/partsui -I$(top_builddir)/corba/partsui'
AC_SUBST(PARTSUI_INCLUDES)

PARTS_INCLUDES='-I$(top_srcdir)/corba/parts -I$(top_builddir)/corba/parts'
AC_SUBST(PARTS_INCLUDES)

LIB_KOM='$(top_builddir)/corba/kom/libkom.la'
AC_SUBST(LIB_KOM)

idldir="\$(includedir)/idl"
AC_SUBST(idldir)

LIB_KDECORE='$(top_builddir)/kdecore/libkdecore.la'
AC_SUBST(LIB_KDECORE)
LIB_KDEUI='$(top_builddir)/kdeui/libkdeui.la'
AC_SUBST(LIB_KDEUI)
LIB_KFORMULA='$(top_builddir)/kformula/libkformula.la'
AC_SUBST(LIB_KFORMULA)
LIB_KFM='$(top_builddir)/kfm/libkfm.la'
AC_SUBST(LIB_KFM)
LIB_KDEUTIL='$(top_builddir)/kdeutils/libkdeutil.la'
AC_SUBST(LIB_KDEUTIL)
LIB_KIO='$(top_builddir)/kio/libkio.la $(LIB_KFILE)'
AC_SUBST(LIB_KIO)
LIB_KFILE='$(top_builddir)/kfile/libkfile.la'
AC_SUBST(LIB_KFILE)
LIB_KAB='$(top_builddir)/kab/libkab.la'
AC_SUBST(LIB_KAB)
LIB_MEDIATOOL='$(top_builddir)/mediatool/libmediatool.la'
AC_SUBST(LIB_MEDIATOOL) 
LIB_KHTML='$(top_builddir)/khtml/libkhtml.la'
AC_SUBST(LIB_KHTML)
LIB_KIMGIO='$(top_builddir)/kimgio/libkimgio.la'
AC_SUBST(LIB_KIMGIO)

dnl CXXFLAGS="$CXXFLAGS -ansi -pedantic"
KDE_CHECK_ANSI

AC_LANG_C
AC_LIBLTDL

AC_ARG_ENABLE(new-stuff, [  --enable-new-stuff      compile the experimental code too (none currently)], [
    if test "$enableval" = "no";
      then ac_new_stuff="no"
    else ac_new_stuff="yes"
  fi
  ], [ac_new_stuff="no"]
  )
  
if test "$ac_new_stuff" = "yes"; then
  EXTRA_SUBDIRS="" dnl put here experimental stuff, if any
else
  EXTRA_SUBDIRS=""
fi

AC_ARG_ENABLE(tutorials, [  --enable-tutorials      compile the tutorials too], [
    if test "$enableval" = "no";
      then ac_tutorials="no"
    else ac_tutorials="yes"
  fi
  ], [ac_tutorials="no"]
  )
 
if test "$ac_tutorials" = "yes"; then
  TUTORIALS="tutorials"
else
  TUTORIALS=""
fi

all_includes='-I$(top_srcdir)/kdecore -I$(top_srcdir)/kdeui $(QT_INCLUDES) $(X_INCLUDES) $(KDE_INCLUDES) $(USER_INCLUDES)'
AC_SUBST(all_includes)

AC_SUBST(EXTRA_SUBDIRS)
AC_SUBST(TUTORIALS)

AC_OUTPUT(Makefile \
	kio/Makefile \
	kdecore/Makefile \
	kdeui/Makefile \
	kimgio/Makefile \
	khtml/Makefile \
	kdetest/Makefile \
	kfile/Makefile \
	kdeutils/Makefile \
	kio/Makefile \
	jscript/Makefile \
	corba/Makefile \
	corba/kom/Makefile \
	corba/kded/Makefile \
	corba/partsui/Makefile \
	corba/parts/Makefile \
	corba/python/Makefile \
	corba/python/idl/Makefile \
	corba/pykde/Makefile \
	corba/kpython/Makefile \
	corba/idl/Makefile \
	corba/tutorials/Makefile \
	corba/tutorials/composing/Makefile \
	corba/tutorials/signals/Makefile \
	corba/tutorials/events/Makefile \
	corba/tutorials/embedding1/Makefile \
	corba/tutorials/embedding2/Makefile \
	corba/tutorials/kded/Makefile \
	kfmlib/Makefile \
	mediatool/Makefile \
	kspell/Makefile \
	kab/Makefile \
	kformula/Makefile \
	toolbar/Makefile \
	doc/Makefile \
	doc/kfile/Makefile \
	doc/kspell/Makefile )

