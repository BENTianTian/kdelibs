# Original Author was Kalle@kde.org
# I lifted it in some mater. (Stephan Kulow)
# I used much code from Janos Farkas

dnl Process this file with autoconf to produce a configure script.
AC_INIT(acinclude.m4) dnl a source file from your sub dir

dnl This is so we can use kde-common
AC_CONFIG_AUX_DIR(admin)

AC_CANONICAL_SYSTEM
AC_ARG_PROGRAM

dnl Automake doc recommends to do this only here. (Janos)
AM_INIT_AUTOMAKE(kdelibs, 2.0pre) dnl searches for some needed programs

AC_PROG_INSTALL

dnl make $KDEDIR the default for the installation
AC_PREFIX_DEFAULT(${KDEDIR:-/usr/local/kde})

dnl generate the config header
AM_CONFIG_HEADER(config.h) dnl at the distribution this done

dnl Checks for programs.
AC_CHECK_COMPILERS
KDE_COMPILER_REPO
AC_ENABLE_SHARED(yes)
AC_ENABLE_STATIC(no)
AC_LIBTOOL_DLOPEN
KDE_PROG_LIBTOOL

dnl for NLS support. Call them in this order!
dnl WITH_NLS is for the po files, GNU_GETTEXT for the sources
AM_KDE_WITH_NLS
AM_KDE_GNU_GETTEXT

dnl Checks for header files.
KDE_CHECK_STL
AC_HEADER_DIRENT
AC_HEADER_STDC
AC_CHECK_HEADERS(fcntl.h sys/time.h unistd.h sys/cdefs.h fnmatch.h sysent.h strings.h sys/stat.h sys/select.h sys/socket.h linux/socket.h socketbits.h sigaction.h paths.h malloc.h monetary.h)

dnl Checks for libraries.
AC_BASE_PATH_KDE([don't test]) dnl kdelibs is a special case
AC_CREATE_KFSSTND(default)
AC_SUBST_KFSSTND
AC_CHECK_LIB(compat, main, [LIBCOMPAT="-lcompat"]) dnl for FreeBSD

dnl Image libraries
KDE_CREATE_LIBS_ALIASES
AC_FIND_QIMGIO
KDE_CHECK_KIMGIO

AM_CONDITIONAL(HAVE_LIBJPEG, test -n "$LIBJPEG" )
AM_CONDITIONAL(HAVE_LIBPNG, test -n "$LIBPNG" )
AM_CONDITIONAL(HAVE_LIBTIFF, test -n "$LIBTIFF")

# configure would do this very late. Too late for us!
test "x$prefix" = xNONE && prefix=$ac_default_prefix

AC_DEFINE_UNQUOTED(KDEDIR, "$prefix", [The prefix to use as fallback])

ac_cpp_safe=$ac_cpp
ac_cpp='$CXXCPP $CPPFLAGS $X_INCLUDES'
AC_CHECK_HEADERS(X11/extensions/shape.h)
ac_cpp=$ac_cpp_safe

ac_cpp_safe=$ac_cpp
ac_cpp='$CXXCPP $CPPFLAGS $X_INCLUDES'
AC_CHECK_HEADERS(X11/ICE/ICElib.h)
ac_cpp=$ac_cpp_safe

dnl Checks for typedefs, structures, and compiler characteristics.
AC_HEADER_TIME

dnl check if the compiler has bool
AC_CHECK_BOOL
dnl AC_CHECK_GNU_EXTENSIONS

dnl Checks for library functions.
AC_CHECK_FUNCS(socket vsnprintf seteuid setegid random strfmon stpcpy)
AC_FUNC_VFORK
AC_CHECK_USLEEP
AC_CHECK_SETENV
AC_CHECK_GETDOMAINNAME
AC_CHECK_GETHOSTNAME
AC_CHECK_RANDOM
AC_CHECK_S_ISSOCK
AC_CHECK_KSIZE_T

dnl KDE_CHECK_MICO()
dnl IDL_DEPENDENCIES='$(IDL) $(top_srcdir)/corba/cuteidl/sti/CUTE.h'
dnl AC_SUBST(IDL_DEPENDENCIES)
dnl IDL='$(top_builddir)/corba/cuteidl/cuteidl'
dnl AC_SUBST(IDL)
dnl KDE_CHECK_PYTHON(1.5)

if test -n "$qt_includes"; then
  QNAMESPACE_H="$qt_includes/qnamespace.h"
fi
AC_SUBST(QNAMESPACE_H)

dnl Perform program name transformation
AC_ARG_PROGRAM

dnl output files
topdir=`pwd`
AC_SUBST(x_includes)
AC_SUBST(x_libraries)
AC_SUBST(topdir)
AC_SUBST(LIBSOCKET)
AC_SUBST(LIBCOMPAT)
AC_SUBST(LIBICE)

KOM_INCLUDES='-I$(top_srcdir)/corba/kom -I$(top_builddir)/corba/kom'
AC_SUBST(KOM_INCLUDES)

PARTSUI_INCLUDES='-I$(top_srcdir)/corba/partsui -I$(top_builddir)/corba/partsui'
AC_SUBST(PARTSUI_INCLUDES)

PARTS_INCLUDES='-I$(top_srcdir)/corba/parts -I$(top_builddir)/corba/parts'
AC_SUBST(PARTS_INCLUDES)

LIB_KOM='$(top_builddir)/corba/kom/libkom.la'
AC_SUBST(LIB_KOM)

idldir="\$(includedir)/idl"
AC_SUBST(idldir)

LIB_KDECORE='$(top_builddir)/kdecore/libkdecore.la'
AC_SUBST(LIB_KDECORE)
LIB_KDEUI='$(top_builddir)/kdeui/libkdeui.la'
AC_SUBST(LIB_KDEUI)
LIB_KFORMULA='$(top_builddir)/kformula/libkformula.la'
AC_SUBST(LIB_KFORMULA)
LIB_KIO='$(top_builddir)/kio/libkio.la'
AC_SUBST(LIB_KIO)
LIB_KFILE='$(top_builddir)/kfile/libkfile.la'
AC_SUBST(LIB_KFILE)
LIB_KAB='$(top_builddir)/kab/libkab.la'
AC_SUBST(LIB_KAB)
LIB_MEDIATOOL='$(top_builddir)/mediatool/libmediatool.la'
AC_SUBST(LIB_MEDIATOOL)
LIB_KHTML='$(top_builddir)/khtml/libkhtml.la'
AC_SUBST(LIB_KHTML)
LIB_KIMGIO='$(top_builddir)/kimgio/libkimgio.la'
AC_SUBST(LIB_KIMGIO)
LIB_DCOP='$(top_builddir)/dcop/libDCOP.la'
AC_SUBST(LIB_DCOP)

AC_LANG_C
kde_save_cc=$CC
AC_LIBLTDL
CC=$kde_save_cc

AC_ARG_ENABLE(tutorials, [  --enable-tutorials      compile the tutorials too], [
    if test "$enableval" = "no";
      then ac_tutorials="no"
    else ac_tutorials="yes"
  fi
  ], [ac_tutorials="no"]
  )

if test "$ac_tutorials" = "yes"; then
  TUTORIALS="tutorials"
else
  TUTORIALS=""
fi

all_includes='-I$(top_srcdir)/kdecore/qk -I$(top_srcdir)/dcop -I$(top_srcdir)/kdecore -I$(top_srcdir)/kdeui $(QT_INCLUDES) $(X_INCLUDES) $(KDE_INCLUDES) $(USER_INCLUDES)'
AC_SUBST(all_includes)

AC_SUBST(EXTRA_SUBDIRS)
AC_SUBST(TUTORIALS)

AC_MSG_CHECKING(if malloc debugging is wanted)
AC_ARG_WITH(dmalloc,
[  --with-dmalloc          use dmalloc, as in
			  http://www.dmalloc.com/dmalloc.tar.gz],
[if test "$withval" = yes; then
  AC_MSG_RESULT(yes)
  AC_DEFINE(WITH_DMALLOC,1,
            [Define if using the dmalloc debugging malloc package])
  LIB_DMALLOC="-ldmalloc"
  LDFLAGS="$LDFLAGS -g"
else
  AC_MSG_RESULT(no)
fi], [AC_MSG_RESULT(no)])

AC_SUBST(LIB_DMALLOC)

ICE_SUBDIR=ICE
ICE_RLIB=ICE/libkICE.la
AC_SUBST(ICE_SUBDIR)
AC_SUBST(ICE_RLIB)

AC_OUTPUT(Makefile \
	kdecore/Makefile \
	kdecore/qk/Makefile \
	kdecore/tests/Makefile \
	kdeui/Makefile \
	kimgio/Makefile \
	khtml/Makefile \
	khtml/java/Makefile \
	khtml/kjs/Makefile \
	kdetest/Makefile \
	kfile/Makefile \
	kio/Makefile \
	kio/file/Makefile \
	kio/kded/Makefile \
	kio/tests/Makefile \
	corba/Makefile \
	corba/cuteidl/Makefile \
	corba/cuteidl/sti/Makefile \
	corba/cuteidl/test/Makefile \
	corba/kom/Makefile \
	corba/kded/Makefile \
	corba/partsui/Makefile \
	corba/parts/Makefile \
	corba/interfaces/Makefile \
	corba/tutorials/Makefile \
	corba/tutorials/composing/Makefile \
	corba/tutorials/signals/Makefile \
	corba/tutorials/events/Makefile \
	corba/tutorials/embedding1/Makefile \
	corba/tutorials/embedding2/Makefile \
	corba/tutorials/kded/Makefile \
	mediatool/Makefile \
	kspell/Makefile \
	kab/Makefile \
	kformula/Makefile \
	kformula/pics/Makefile \
	toolbar/Makefile \
	doc/Makefile \
	doc/kfile/Makefile \
	doc/kdebug/Makefile \
	doc/kspell/Makefile \
	doc/kded/Makefile \
	doc/komop/Makefile \
	mimetypes/Makefile \
	mimetypes/application/Makefile \
	mimetypes/audio/Makefile \
	mimetypes/image/Makefile \
	mimetypes/inode/Makefile \
	mimetypes/text/Makefile \
	mimetypes/video/Makefile \
	kstyles/Makefile \
	kstyles/basicplugin/Makefile \
	kstyles/stepplugin/Makefile \
	kstyles/utils/Makefile \
	kstyles/utils/mktheme/Makefile \
	kstyles/utils/settheme/Makefile \
	kstyles/themes/Makefile \
	kstyles/pics/Makefile \
	kidl/Makefile \
	kidl2cpp/Makefile \
	kparts/Makefile \
	interfaces/Makefile \
	dcop/Makefile \
	dcop/ICE/Makefile )


